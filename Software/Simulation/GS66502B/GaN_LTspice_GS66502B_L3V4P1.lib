* Created in LTspice Version XVII                               *
*                                            					*
* GaN Systems Inc. Power Transistors                            *
* LTSpice Library for GaN Transistors                           *
* Version 4.1                                                   *
*                                                               *
*****************************************************************
*****************************************************************
*                                                               *
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as                                           *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    *
* typical device.                                               *
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*                                                               *
* This library contains models of the following GaN Systems     *
* Inc. transistors:                                             *
*                                                               *
*   GS66502B                                                    *
*****************************************************************
*Level 3. In addition to Level 2, this level includes the stray inductance in the package.
*$
*$
.subckt GaN_LTspice_GS66502B_L3V4P1 gatein drainin sourcein source_S TC TJ
*
*
.param 	sf = 0.225
.param rTC=-0.004  gan_res={40.7e-3}		metal_res={3.2e-3}   gtc=2.815 sh_s = 0.05263   sh_d = 0.94376
.param  cur=.099   x0_0=1.1	  x0_1=1.1  x0_2=1.0  thr = 1.61  itc=0.415 atc=98.33358
*
*
Cth_1 0 TJ 		{2.0e-5}
Cth_2 0 T11  	{1.85e-4}
Cth_3 0 T22 	{1.63e-3}
Cth_4 0 T33 	{5.0e-3}
*
*
Rth_1 T11 TJ 	{0.06}
Rth_2 T22 T11 	{0.92}
Rth_3 T33 T22 	{0.96}
Rth_4 TC T33 	{0.06}
*
*
bdtemp 0 TJ I = (if(v(drain,source)>0,
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source)-thr)))*
+	v(drain,source)/(1 + max(x0_0+x0_1*(v(gate,source)+x0_2),0.2)*v(drain,source)))*
+	v(drainin,sourcein),
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain)-thr)))*
+	v(source, drain)/(1 + max(x0_0+x0_1*(v(gate,drain)+x0_2),0.2)*v(source,drain)))*
+	v(sourcein, drainin))) * sf
*
*
ld drainin drain3 {170e-12} Rser=0
ERES_d drain3 3_d value={I(VSENSE_d)*sh_d* (metal_res*(1-1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc)) / sf }
VSENSE_d 3_d drain DC 0
*
*
VSENSE_s 3_s source DC 0
ERES_s source3 3_s value={I(VSENSE_s)* sh_s * (metal_res*(1-1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc)) / sf}
Lcs source3 source3b {20e-12}
Ls sourcein source3b {150e-12} Rser=0
*
*
RSS source_S1 source3b {0.001 * sf}
LSS source_S source_S1 {0.2e-9 * sf} Rser=0
rg gatein gate1 {1.0 * sf}
Lg gate1 gate {3.1e-9 * sf} Rser=0
*
*
Rcsdconv drain source {4000Meg}
Rcgsconv gate source {4000Meg}
Rcgdconv gate drain {4000Meg}
*
*
bswitch drain2 source2 I = (if(v(drain2,source2)>0,
+   						(cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source2)-thr)))*
+							v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+x0_2),0.2)*v(drain2,source2))),
+   						(-cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain2)-thr)))*
+							v(source2, drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+x0_2),0.2)*v(source2,drain2))))) * sf
*
*
R_drain2 drain2 drain {(1e-4)}
R_source2 source2 source {(1e-4)}
*
*
C_GS	gate source  	{265.3e-12 * sf}
C_GS1	gate source 	Q = sf * (-0.9431e-12*(1-1./(1+exp(0.0177*(-v(drain, source)+300.03))))
+						  		-0.4182e-10*(1-1./(1+exp(0.0700*(-v(drain, source)+33.3))))
+						 		-0.5657e-10*(1-1./(1+exp(0.277*(-v(drain, source)+1.1))))
+								-(-0.435*90.48e-12*(1-1./(1+exp(0.06*(-v(drain, source)+1.1))))
+                        		-0.435*0.54e-10*(1-1./(1+exp(0.9*(-v(drain, source)+2.1))))))*x
C_GS2 	gate source   	Q = 0.464*2.23e-010*log(1+exp(6.5*(x-1.55))) * sf
*
*
C_GD   	gate drain  	{1.8e-012 * sf}
C_GD1  	gate drain 		Q = 0.444*4.6e-10*log(1+exp(0.277*(x+1.1))) * sf + 0.444*13.2e-10*log(1+exp(0.070*(x+33.3))) * sf + 0.444*1.2e-10*log(1+exp(0.0177*(x+300.03))) * sf
*
*
C_SD	source drain  	{0.8e-010 * sf}
C_SD1	source drain 	Q =  0.444*4.4e-9*log(1+exp(.15*(x+68))) * sf + 0.444*6.56e-9*log(1+exp(.03*(x+180))) * sf 
*
*
.ends
*$